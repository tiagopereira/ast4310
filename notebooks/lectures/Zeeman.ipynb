{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "0469bd99-e2d1-408c-a375-107dbe9c48d6",
   "metadata": {},
   "outputs": [],
   "source": [
    "using CairoMakie\n",
    "using Unitful\n",
    "using Colors\n",
    "import PhysicalConstants.CODATA2018: e, m_e, c_0"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "b4f09b55-009b-452b-9295-b72c650dfdb4",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "plot_zeeman"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "\"\"\"\n",
    "Calculates Landé factor from LS coupling.\n",
    "\"\"\"\n",
    "function g_LS(L, S, J)\n",
    "    if J == 0\n",
    "        return 1.\n",
    "    else \n",
    "        return 1 + 0.5 * (J * (J + 1) + S * (S + 1) - L * (L + 1)) / (J * (J + 1))\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Calculates the effective Landé factor.\n",
    "\"\"\"\n",
    "function g_eff(g1, g2, J1, J2)\n",
    "    d = J1 * (J1 + 1) - J2 * (J2 + 1)\n",
    "    return 0.5 * (g1 + g2) + 0.25 * (g1 - g2) * d\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Parses term symbol in the form ^2S+1 L _J., \n",
    "where multiplicity is 2S + 1, letter is the letter of the term (SPDF...)\n",
    "and J is J.\n",
    "\"\"\"\n",
    "function parse_term(multiplicity, letter, J)\n",
    "    S = (multiplicity - 1) / 2\n",
    "    terms = Dict(c => i - 1 for (i, c) in enumerate(\"SPDFGHIJK\"))\n",
    "    ch = uppercase(string(letter))[1]\n",
    "    L = terms[ch]\n",
    "    return (L, S, J)\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Calculates strengths of Zeeman components.\n",
    "\"\"\"\n",
    "function zeeman_strength(J_l, J_u, M_l, M_u)\n",
    "    J = J_l\n",
    "    M = M_l\n",
    "    ΔM = M_u - M_l\n",
    "    ΔJ = J_u - J_l\n",
    "    if ΔM == 1\n",
    "        if ΔJ == 1\n",
    "            return (3 * (J + M + 1) * (J + M + 2)) / (2 * (J + 1) * (2J + 1) * (2J + 3))\n",
    "        elseif ΔJ == 0\n",
    "            return (3 * (J - M) * (J + M + 1)) / (2 * J * (J + 1) * (2J + 1))\n",
    "        elseif ΔJ == -1\n",
    "            return (3 * (J - M) * (J - M - 1)) / (2 * J * (2J - 1) * (2J + 1))\n",
    "        else\n",
    "            throw(ArgumentError(\"Invalid transition: ΔJ must be -1, 0, or 1\"))\n",
    "        end\n",
    "    elseif ΔM == 0\n",
    "        if ΔJ == 1\n",
    "            return (3 * (J - M + 1) * (J + M + 1)) / ((J + 1) * (2J + 1) * (2J + 3))\n",
    "        elseif ΔJ == 0\n",
    "            return 3 * M^2 / (J * (J + 1) * (2J + 1))\n",
    "        elseif ΔJ == -1\n",
    "            return (3 * (J - M) * (J + M)) / (J * (2J - 1) * (2J + 1))\n",
    "        else\n",
    "            throw(ArgumentError(\"Invalid transition: ΔJ must be -1, 0, or 1\"))\n",
    "        end\n",
    "    elseif ΔM == -1\n",
    "        if ΔJ == 1\n",
    "            return (3 * (J - M + 1) * (J - M + 2)) / (2 * (J + 1) * (2J + 1) * (2J + 3))\n",
    "        elseif ΔJ == 0\n",
    "            return (3 * (J + M) * (J - M + 1)) / (2 * J * (J + 1) * (2J + 1))\n",
    "        elseif ΔJ == -1\n",
    "            return (3 * (J + M) * (J + M - 1)) / (2 * J * (2J - 1) * (2J + 1))\n",
    "        else\n",
    "            throw(ArgumentError(\"Invalid transition: ΔJ must be -1, 0, or 1\"))\n",
    "        end\n",
    "    else\n",
    "        throw(ArgumentError(\"Invalid transition: ΔM must be -1, 0, or 1\"))\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "zeeman_broadening(λ0, B) = (e * λ0^2 * B / (4π * m_e * c_0)) |> u\"nm\"\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Calculates energy separation of different Zeeman\n",
    "components (in units of mu_B * B),\n",
    "and the strength of Zeeman components.\n",
    "\"\"\"\n",
    "function zeeman_components(upper_term::Tuple, lower_term::Tuple)\n",
    "    L_u, S_u, J_u = parse_term(upper_term...)\n",
    "    L_l, S_l, J_l = parse_term(lower_term...)\n",
    "    #  Find possible combinations\n",
    "    M_u = J_u == 0 ? [zero(J_u)] : collect(-J_u:one(J_u):J_u)\n",
    "    M_l = J_l == 0 ? [zero(J_l)] : collect(-J_l:one(J_l):J_l)\n",
    "    g_u = g_LS(L_u, S_u, J_u)\n",
    "    g_l = g_LS(L_l, S_l, J_l)\n",
    "    Mu_col = reshape(M_u, :, 1)     \n",
    "    Ml_row = reshape(M_l, 1, :)  \n",
    "    ΔM = Mu_col .- Ml_row     \n",
    "    permitted = abs.(ΔM) .<= 1    # electric dipole selection rule\n",
    "    energy_diff = g_u .* Mu_col .- g_l .* Ml_row\n",
    "    # redefine in matrix form\n",
    "    Mu_mat = ΔM .+ Ml_row\n",
    "    Ml_mat = Mu_col .- ΔM\n",
    "    result = Dict{Symbol, NamedTuple}()\n",
    "    for (q, m) in zip((:σr, :π, :σb), (-1, 0, 1))\n",
    "        q_index = permitted .& (ΔM .== m)\n",
    "        energies = energy_diff[q_index]\n",
    "        # Get only unique energies for a given Mj\n",
    "        unique_vals = unique(energies)\n",
    "        first_idxs = [findfirst(==(v), energies) for v in unique_vals]\n",
    "        energies_unique = unique_vals\n",
    "        Mu_unique = Mu_mat[q_index][first_idxs]\n",
    "        Ml_unique = Ml_mat[q_index][first_idxs]\n",
    "        strengths = [zeeman_strength(J_l, J_u, ml, mu) for (ml, mu) in zip(Ml_unique, Mu_unique)]\n",
    "        result[q] = (energy = energies_unique, strength = strengths)\n",
    "    end\n",
    "    return result\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Plots Zeeman components in energy vs strength following\n",
    "the usual convention that pi are up and sigma are down.\n",
    "\"\"\"\n",
    "function plot_zeeman(upper_term, lower_term; plot_geff=false)\n",
    "    fig = Figure(size=(400,300))\n",
    "    ax = Axis(fig[1,1], xgridvisible=false, ygridvisible=false)\n",
    "    hidedecorations!(ax; grid=false)\n",
    "    hidespines!(ax)\n",
    "    cfg = Dict(:σr => (:red, -1), :π => (:black, 1), :σb => (:blue, -1))\n",
    "    for (component, value) in zeeman_components(upper_term, lower_term)\n",
    "        for (e, s) in zip(value.energy, value.strength)\n",
    "            lines!([-e, -e], [0, s * cfg[component][2]], color=cfg[component][1], linewidth=2)\n",
    "        end\n",
    "    end\n",
    "    hlines!(ax, 0, color=:black, linewidth=2)\n",
    "    if plot_geff\n",
    "        L_u, S_u, J_u = parse_term(upper_term...)\n",
    "        L_l, S_l, J_l = parse_term(lower_term...)\n",
    "        g_u = g_LS(L_u, S_u, J_u)\n",
    "        g_l = g_LS(L_l, S_l, J_l)\n",
    "        geff = g_eff(g_l, g_u, J_l, J_u)\n",
    "        vlines!(ax, [-geff, geff], color=:yellow3, linestyle=:dash, linewidth=2)\n",
    "    end\n",
    "    return fig\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "723444e7",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.7",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
